/*
 *    Copyright 2014. Binh Nguyen
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.0"
    }
}

plugins {
    id 'groovy'
    id 'maven-publish'
}

version = '3.0.1'
group = 'com.github.alisiikh'

ext {
    scalaVersion = '2.12'
}

wrapper {
    gradleVersion = '4.7'
}

apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    projectGithub = 'https://github.com/alisiikh/gradle-scalastyle-plugin'

    pluginId = 'com.github.alisiikh.scalastyle'
    pluginDescription = 'Gradle plugin for scalastyle'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    testImplementation gradleTestKit()
    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testImplementation 'commons-io:commons-io:2.5'
}

test {
    dependsOn 'publishToMavenLocal'

    testLogging {
        showStandardStreams = true
    }
    systemProperty 'SCALA_VERSION', scalaVersion
    systemProperty 'PLUGIN_VERSION', version
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

pluginBundle {
    mavenCoordinates {
        groupId = project.group
    }

    website = projectGithub
    vcsUrl = projectGithub
    description = pluginDescription
    tags = ['scalastyle', 'scala', 'code analysis', 'checkstyle']

    plugins {
        scalastylePlugin {
            id = pluginId
            displayName = pluginDescription
        }
    }
}
